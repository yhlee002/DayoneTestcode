plugins {
    id 'java'
    id 'org.springframework.boot' version '3.2.3'
    id 'io.spring.dependency-management' version '1.1.4'
    id 'jacoco'
    id 'org.sonarqube' version '4.0.0.2929'
    id 'com.diffplug.spotless' version '6.12.0'
}

group = 'com.jyujyu.dayonetest'
version = '0.0.1-SNAPSHOT'

java {
    sourceCompatibility = '17'
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    compileOnly 'org.projectlombok:lombok'
    runtimeOnly 'com.mysql:mysql-connector-j'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    annotationProcessor 'org.projectlombok:lombok'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'

    testImplementation "org.mockito:mockito-core:3.+"
    testImplementation "org.testcontainers:testcontainers:1.19.0"

    implementation 'org.springframework.boot:spring-boot-starter-data-redis'
    testImplementation "com.redis.testcontainers:testcontainers-redis-junit:1.6.4"

    implementation(platform("software.amazon.awssdk:bom:2.20.136"))
    implementation("commons-io:commons-io:2.13.0")
    implementation("software.amazon.awssdk:aws-core")
    implementation("software.amazon.awssdk:sdk-core")
    implementation("software.amazon.awssdk:sts")
    implementation("software.amazon.awssdk:s3")
    testImplementation("org.testcontainers:localstack:1.19.0")

    implementation("org.springframework.kafka:spring-kafka")
    testImplementation("org.testcontainers:kafka:1.19.0")

    testImplementation 'com.tngtech.archunit:archunit:1.1.0'
}

jacoco {
    toolVersion = "0.8.8";
}

tasks.named('test') {
    useJUnitPlatform()

    jacoco {}
    finalizedBy(tasks.jacocoTestReport)
}

tasks.jacocoTestReport {
    reports {
        xml.required = true
        html.required = true
        csv.required = false

        xml.destination(file("build/jacoco/jacoco.xml"))
        html.destination(file("build/jacoco/jacoco.html"))
    }
}

tasks.jacocoTestCoverageVerification {
    violationRules {
        rule {
            enabled = true
            element = "CLASS"

            // line coverage가 50% 이상이어야 한다.
            limit {
                counter = "LINE"
                value = "COVEREDRATIO"
                minimum = BigDecimal.valueOf(0.5) // 최솟값
            }

            // total line이 최대 100줄이다. (=한 클래스는 100줄 이상의 소스 코드를 가질 수 없다.)
            limit {
                counter = "LINE"
                value = "TOTALCOUNT"
                maximum = BigDecimal.valueOf(100) // 최댓값
            }

            excludes = List.of(
                    "*.controller.*",
                    "com.jyujyu.dayonetest.MyCalculatorApplication",
                    "com.jyujyu.dayonetest.DayonetestApplication"
                    // 컨트롤러와 메인함수가 있는 클래스는 커버리지 검사 제외
            )
        }
    }
}

sonar {
    properties {
        property("sonar.host.url", "http://localhost:9000")
        property("sonar.login", "sqp_c3256f1babeaeff69b6d09de0d54166d3a655ffd")
        property("sonar.sources", "src/main/java")
        property("sonar.tests", "src/test/java")
        property("sonar.sourceEncoding", "UTF-8")
        property("sonar.projectKey", "dayonetest")
        property("sonar.projectName", "dayonetest")
        property("sonar.coverage.jacoco.xmlReportPaths", "build/jacoco/jacoco.xml")
    }
}

spotless {
    java {
        googleJavaFormat()

        removeUnusedImports() // 사용하지 않는 import 제거
        trimTrailingWhitespace() // 공백이 2칸이상 존재하거나, 앞 뒤로 불필요한 공백 존재시 제거
        indentWithSpaces(4) // 들여쓰기가 탭이 아닌 띄어쓰기(4칸)으로 변경
        endWithNewline() // 마지막 줄을 공백으로 종료
    }
}

tasks.register("addGitPreCommitHook", Copy) {
    from "script/pre-commit"
    into ".git/hooks"
}
